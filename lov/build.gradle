apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'org.jetbrains.dokka'
apply plugin: 'com.jfrog.bintray'

version = "0.1.0"
group = "net.niiranen"

def siteUrl = 'https://github.com/mattiasniiranen/permission'
def gitUrl = 'https://github.com/mattiasniiranen/permission.git'

android {
    compileSdkVersion 23
    buildToolsVersion '23.0.2'

    defaultConfig {
        minSdkVersion 8
        targetSdkVersion 23
        versionCode 1
        versionName "0.1.0"

        consumerProguardFiles 'proguard-rules.pro'

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            debuggable false
        }
        debug {
            minifyEnabled false
            debuggable true
        }
    }

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
        test.java.srcDirs += 'src/test/kotlin'
        androidTest.java.srcDirs += 'src/androidTest/kotlin'
    }

    testOptions {
        unitTests.returnDefaultValues = true
    }
}

dependencies {
    compile 'com.android.support:appcompat-v7:23.2.1'
    compile 'io.reactivex:rxjava:1.1.1'
    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    compile "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"

    testCompile 'junit:junit:4.12'
    androidTestCompile 'com.android.support.test:runner:0.5'
    androidTestCompile 'com.android.support.test:rules:0.5'
    androidTestCompile 'com.android.support.test.espresso:espresso-core:2.2.2'
    androidTestCompile 'com.android.support.test.uiautomator:uiautomator-v18:2.1.2'
}

// Needed for dokka
sourceSets {
    main.java.srcDirs += 'src/main/kotlin'
    test.java.srcDirs += 'src/test/kotlin'
}
dokka {
    moduleName = 'Lov'
    outputFormat = 'html'
    samples = ['src/test/kotlin']
    // linkMapping is broken in 0.9.7, https://github.com/Kotlin/dokka/issues/52
    //linkMapping {
    //    dir = "src/main/kotlin"
    //    url = "https://github.com/mattiasniiranen/lov/blob/master/src/main/kotlin"
    //    suffix = "#L"
    //}
}

Properties properties = new Properties()
if (project.rootProject.file('local.properties').exists()) {
    properties.load(project.rootProject.file('local.properties').newDataInputStream())
}

bintray {
    user = properties.getProperty("bintray.user")
    key = properties.getProperty("bintray.apiKey")

    configurations = ['archives']
    pkg {
        repo = 'maven'
        name = 'lov'
        desc = 'Permission library for Android'
        websiteUrl = siteUrl
        issueTrackerUrl = 'https://github.com/mattiasniiranen/lov/issues'
        vcsUrl = gitUrl
        licenses = ['Apache-2.0']
        labels = ['aar', 'android']
        publicDownloadNumbers = true

        githubRepo = 'mattiasniiranen/lov'
        githubReleaseNotesFile = 'README.md'

        version {
            name = version
            desc = "Lov version $version"
            released = new Date()
            gpg {
                sign = properties.getProperty("gpg.sign").equals("true")
                passphrase = properties.getProperty("gpg.passphrase")
            }
        }
    }
}

install {
    repositories.mavenInstaller {
        pom.project {
            packaging 'aar'
            name 'Permission library for Android'
            url siteUrl
            licenses {
                license {
                    name 'The Apache Software License, Version 2.0'
                    url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    distribution 'repo'
                }
            }
            developers {
                developer {
                    id 'mattiasniiranen'
                    name 'Mattias Niiranen'
                    email 'mattias@niiranen.net'
                }
            }
            scm {
                connection gitUrl
                developerConnection gitUrl
                url siteUrl

            }
        }
    }
}

task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

task dokkaJar(type: Jar, dependsOn: dokka) {
    classifier = 'dokka'
    from dokka.outputDirectory
}

artifacts {
    archives dokkaJar
    archives sourcesJar
}

task findConventions << {
    println project.getConvention()
}
